extends Boot;
native Tonyu;
\config() {
    $config={
        "spr.scale"=1,
        //"crashTo.size"=32,
        "obj.limits"={
        },
        "th.count"=20,
        // unimplemented: the variables treated as double(common to all classes) 
        "defdbl": [
        ],
        // global constants, either object literal or function
        "consts":{
            "$rtn_test":10,
        },
        // global int arrays
        "intArrays": {
        },
        // unimplemented: global strings(char*). length cannot be changed.
        "chars": {
        }
    };
}
let kanaTbl,dac10,dac5;
\createMainObject() {
    //--config section---
    config();
    let spad=($Navigator.isMobile());


    if(typeof $config.consts=="function"){
        $config.consts=$config.consts();
    }
    $config.consts=$config.consts||{};
    $config.intArrays=$config.intArrays||{};
    $config.postprocess=$config.postprocess||{};
    //--------
    getMainClass();
    kanaTbl='○●'+
    'をぁぃぅぇぉゃゅょっ?あいうえおかきくけこさしすせそ?'+
    '。「」、・ヲァィゥェォャュョッーアイウエオカキクケコサシスセソ'+
    'タチツテトナニヌネノハヒフヘホマミムメモヤユヨラリルレロワン'+
    '゛゜たちつてとなにぬねのはひふへほまみむめもやゆよらりるれろわん';
    dac10="がぎぐげござじずぜぞ"+
    "だぢづでどばびぶべぼ";
    dac5="ぱぴぷぺぽ";
    
    $RSprPat=new RSprPat;
    if (!$config.postprocess.noPreview) {
        $excludeFromAll=$Screen.all();
        $Screen.resize(512,512+
        (spad?200:0));
        $MScreen=new Screen{x:256,y:224,width:256,height:192,scaleX:2};
        $MLayer=$MScreen.addLayer();
    
        $MScreen.setBGColor($RSprPat.palette[3]);
        $Screen.setBGColor($RSprPat.palette[6]);
        $map=new Map2{chipWidth:8,chipHeight:8,row:24,col:32,layer:$MLayer, x:0,y:0,
            curx:0,cury:0,
        };
    }

    $crashTo_size=8<<$config["spr.scale"];
    $screenWidth=256<<$config["spr.scale"];
    $screenHeight=192<<$config["spr.scale"];
    $builtinGlobals={
        $screenWidth,
        $screenHeight,
        $frameCount: 0,
    };
    for (let k,v of $config.consts) {
        Tonyu.setGlobal(k,v);
    }
    Object.assign($config.consts){
        $screenWidth,
        $screenHeight,
    };
    $mapper=new Mapper;
    if (!$mapper) throw new Error("Mapper is not set");
    $sound.initMMTimer().interval=1/57;
    
    $MZO=new MZO{mapper:$mapper};
    for (var k,v in $MZO.adrs) {
        Tonyu.setGlobal(k,v);
    }

    $pat_font_orig=$pat_font;
    $pat_font=0;
    for (let k,v in $config.intArrays) {
        if (typeof v==="number") {
            let n=v;
            v=[];
            for (let i=0;i<n;i++) v.push(0);
        } else {
            v=v.slice();// TODO matrix deep slice
        }
        Tonyu.setGlobal(k,v);
            //$mapper.assignAry16(v);
    }
    //for (let y=0;y<24;y++) for(let x=0;x<32;x++) $map.set(x,y,$pat_font+35);
    $exporter=new GenAsm;
    let ide=$Boot.getIDE();
    if (ide && $Navigator.isMobile()) ide.runDialog.resize {
        left:10,top:100,width:400,
        height:600
    };
    $mainClassName=mainClass.meta.shortName;
    if ($config.postprocess.noPreview) return;
    if ($editButton) {
        new Button{top:480,text:"Edit Page",height:30, 
            fillStyle:$RSprPat.palette[8],
            onClick:\{$editButton.openEditor();}
        };
    }
    if(spad){
        showPad();
    }
    $PSG=[
    new Beep,new Beep,new Beep
    ];
    $Boot.onInter=\{};
    $sound.on("interval", \{$Boot.onInter();});
    new mainClass;
}
\loadPage(Class) {
    $MLayer.all(RSprite).die();
    new Class;
}
\openDac(s){
    let res="";
    for (let i=0;i<s.length;i++) {
        let j=dac10.indexOf(s [i]);
        if (j>=0){
            res+=String.fromCharCode(
            dac10.charCodeAt(j)-1)+"゛";
            continue;
        }
        j=dac5.indexOf(s [i]);
        if (j>=0){
            res+=String.fromCharCode(
            dac5.charCodeAt(j)-2)+"゜";
            continue;
        }
        res+=s [i];continue;
            
    }
    return res;
}
\str2bin(s){
    let res=[ ];
    s=openDac(s);
    for (let i=0;i<s.length;i++) {
        let c=s.charCodeAt(i);
        if (c>=128) {
            c=kanaTbl.indexOf(s[i]);
            if (c>=0) c+=128+4;else c=32;
        }
        res.push(c);
    }
    return res;
}
\showPad(){
    $pad=new Pad {
        buttonCnt=2
    };
}
super.main();
